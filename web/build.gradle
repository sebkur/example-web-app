buildscript {
    repositories {
        mavenLocal()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'de.topobyte:gradle-generate-favicons-plugin:0.0.2'
        classpath 'de.topobyte:gradle-cache-busting-plugin:0.0.3'
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.7.0-local'
    }
}

plugins {
    id 'com.craigburke.client-dependencies' version '1.4.0'
}

apply plugin: 'de.topobyte.generate-favicons-gradle-plugin'
apply plugin: 'de.topobyte.cache-busting-gradle-plugin'
apply plugin: 'com.bmuschko.tomcat'

dependencies {
    implementation project(':example-webapp-core')

    implementation 'de.topobyte:pagegen-core:1.2.0'
    implementation 'de.topobyte:pagegen-bootstrap:1.3.0'
    implementation 'de.topobyte:webgun-core:1.4.0'
    implementation 'de.topobyte:webgun-resolving:1.4.0'
    implementation 'de.topobyte:jsoup-elements:0.2.1'
    implementation 'de.topobyte:jsoup-bootstrap4:1.1.2'
    implementation 'de.topobyte:jsoup-servlet:0.2.0'
    implementation 'de.topobyte:jsoup-jquery:0.1.0'
    implementation 'de.topobyte:jsoup-utils:0.2.0'
    implementation 'de.topobyte:jsoup-flexmark:0.1.1'
    implementation 'de.topobyte:jsoup-feather:1.2.0'
    implementation 'de.topobyte:servlet-utils:0.0.2'
    implementation 'de.topobyte:css-utils:0.1.0'

    implementation 'com.vladsch.flexmark:flexmark:0.28.18'
    implementation 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.28.18'
    implementation 'com.vladsch.flexmark:flexmark-ext-tables:0.28.18'
    implementation 'com.vladsch.flexmark:flexmark-ext-wikilink:0.28.18'

    implementation 'org.slf4j:slf4j-api:1.7.20'

    providedCompile 'javax.servlet:javax.servlet-api:3.+'
    providedCompile 'javax.servlet.jsp:jsp-api:2.1'

    // we need this as compile due to shutdown code
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'junit:junit:4.12'
}

eclipse {
    wtp {
        component {
            contextPath = '/'
            deployName = 'example-webapp'
            resource deployPath: '/', sourcePath: 'src/main/webapp'
            resource deployPath: '/', sourcePath: 'build/static'
        }
    }
}
eclipse.classpath.file {
    withXml {
        def node = it.asNode()
        node.appendNode('classpathentry', [kind: 'src', output: 'bin/main', path: 'logging/devel'])
        node.appendNode('classpathentry', [kind: 'src', output: 'bin/main', path: 'config/devel'])
    }
    // Classpath entry for Eclipse which changes the order of classpathentries; otherwise no sources for 3rd party jars are shown
    withXml { xml ->
        def node = xml.asNode()
        node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
        node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
    }
}

clientDependencies {
    installDir = 'src/assets/vendor/client'
    copyExcludes = ['**/*.map', '**/Gruntfile.js', 'gulpfile.js', 'source/**']
    yarn {
        'jquery'('3.5.1')
        'bootstrap'('4.5.0')
    }
}

def commonWarDefs = {
    from 'build/static'
}

war {
    classifier = 'production'
    configure commonWarDefs
}

task testingWar (type: War) {
    classifier = 'testing'
    configure commonWarDefs
}

tasks.eclipse.dependsOn clientInstall
tasks.eclipse.dependsOn generateFavicons

tasks.generateStaticFiles.dependsOn generateFavicons

[war, testingWar].each { task ->
    task.dependsOn clientInstall
    task.dependsOn generateFavicons
    task.from ('logging/' + task.archiveClassifier.get()) {
        into('WEB-INF/classes')
        include "**/*"
    }
    task.from ('config/' + task.archiveClassifier.get()) {
        into('WEB-INF/classes')
        include "**/*"
    }
}

favicons {
    input = 'res/images/icon.svg'
}

cacheBusting {
    input = []
    input += 'res'
    input += 'build/favicons'
    input += 'src/assets/vendor'
}

dependencies {
    def tomcatVersion = '8.5.16'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:8.5.2",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
    contextPath = "/"
    resources {
        resource {
            path = new File(projectDir, "src/assets/vendor")
            mountpoint = "/client"
        }
        resource {
            path = new File(projectDir, "build/static")
            mountpoint = "/"
        }
    }
}

[tomcatRun, tomcatRunWar].each { task ->
    task.additionalRuntimeResources << new File(projectDir, "build/static")
    task.additionalRuntimeResources << new File(projectDir, "/logging/devel")
    task.additionalRuntimeResources << new File(projectDir, "/config/devel")
}
